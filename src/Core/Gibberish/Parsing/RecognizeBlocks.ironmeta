using System;
using System.Linq;
using Gibberish.AST;
using Gibberish.AST._1_Bare;

ironmeta RecognizeBlocks<char, LanguageConstruct> : IronMeta.Matcher.Matcher<char, LanguageConstruct>
{
	WholeFile = LanguageConstruct*:matches;
	LanguageConstruct = BlankLine | CommentDefinition | Prelude | Statement;

	Statement = '\t'*:indentation (~(':' | '\n' | '\r') .)*:content NL -> { return _ExtractStatementAndErrors(indentation.Inputs.Count(), content.AsString()); };

	Prelude = '\t'*:indentation (~(':' | '\n' | '\r') .)*:content ":" (~('\n' | '\r') .)*:trailingContent NL -> { return _ExtractPreludeAndErrors(indentation.Inputs.Count(), content.AsString(), trailingContent.AsString()); };

	CommentDefinition = '#' ('[' /[0-9]/+:commentId "]:")?:wholeCommentId (' ' | '\t')*:commentSep (~('\r' | '\n') .)*:content NL -> { return _ExtractSingleLineCommentDefinition(wholeCommentId.Inputs.Count() > 0 ? commentId.AsString() : string.Empty, content.AsString(), commentSep.AsString()); };
	
	BlankLine = '\t'*:indentation /[\t ]/*:invalidWhitespace NL -> { return _ExtractBlankLine(indentation.Inputs.Count(), invalidWhitespace.AsString()); };

	Nothing = (~(.?));
	NL = "\r\n" | "\n" | "\r";
}